apply plugin: 'com.android.application'

// Static version (grabver plugin removed due to broken dependencies)

android {
    namespace 'eu.davidea.samples.flexibleadapter'
    compileSdk rootProject.ext.compileSdk

    defaultConfig {
        applicationId 'eu.davidea.samples.flexibleadapter'
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode rootProject.ext.libraryVersionCode
        versionName rootProject.ext.libraryVersionName

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildTypes {
        debug {
            versionNameSuffix '-debug'
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lint {
        abortOnError false
    }
    buildFeatures {
        dataBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // FlexibleAdapter + extensions
    implementation project(':flexible-adapter')
    implementation project(':flexible-adapter-ui')
    implementation project(':flexible-adapter-databinding')

    // AndroidX Libraries
    implementation androidx.appCompat
    implementation androidx.cardView
    implementation androidx.recyclerView
    implementation google.material
    implementation androidx.swipeRefreshLayout

    // Timber (Logger) - https://github.com/JakeWharton/timber
    implementation libraries.timber

    // Butter Knife (View Injector) - https://github.com/JakeWharton/butterknife
    implementation libraries.butterKnife
    annotationProcessor libraries.butterKnifeCompiler

    // Glide (Image Loader & Caching)
    implementation libraries.glide
    annotationProcessor libraries.glideCompiler

    // Smooth-app-bar-layout
    implementation('me.henrytao:smooth-app-bar-layout:24.1.1.0') {
        exclude group: 'com.android.support'
    }

    // UI Widgets
    implementation widgets.flipView

    // Test libraries
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0-alpha04', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.23.4'

}